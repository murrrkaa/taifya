# Грамматика выражений языка <MarinkaSql>

## Синтаксис выражений
### Выражения могут содержать

- Числовые литералы
- Идентификаторы (переменные, константы)
- Арифметические операторы
- Скобки для группировки выражений
- Вызовы встроенных функций со списком аргументов

## Операторы
### Арифметические операторы
| Символ  | Операция                  |
|---------|---------------------------|
| `+`     | Сложение, унарный плюс    |
| `-`     | Вычитание, унарный минус  |
| `*`     | Умножение                 |
| `/`     | Деление                   |

## Приоритет операторов

| Приоритет (по убыванию) | Операторы           |
|-------------------------|---------------------|
| 3                       | `+`, `-` (унарные)  |
| 2                       | `*`, `/`            |
| 1                       | `+`, `-` (бинарные) |


## Встроенные функции

| Функция          | Назначение                                                 |
|------------------|------------------------------------------------------------|
| `abs(x)`         | Возвращает модуль числа                                    |
| `min(a, b, ...)` | Возвращает наименьшее из переданных чисел                  |
| `max(a, b, ...)` | Возвращает наибольшее из переданных чисел                  |
| `round(x)`       | Округляет число до ближайшего целого                       |
| `ceil(x)`        | Возвращает ближайшее целое, большее или равное переданному |
| `floor(x)`       | Возвращает ближайшее целое, меньшее или равное переданному |

## Поддержка констант

| Константа   | Значение     | Назначение      |
|-------------|--------------|-----------------|
| `Pi`        | 3.1415926535 | Число π         |
| `Euler`     | 2.7182818284 | Число Эйлера e  |

## Грамматика в нотации EBNF

```ebnf

(* Список выражений *)
expression_list = expression, { ",", expression } ;

(* Основное выражение: операции сложения и вычитания *)
expression = term_expression, { ("+" | "-"), term_expression } ;

(* Термы — операции умножения и деления *)
term_expression = factor_expression, { ("*" | "/"), factor_expression } ;

(* Факторы — унарные операции + и - *)
factor_expression = [ "+" | "-" ], simple_expression ;

(* Простые выражения — числа, идентификаторы, функции, константы или подвыражения в скобках *)
simple_expression = number | identifier | constant | function_call | "(", expression, ")" ;

(* Вызовы функций *)
function_call = function_name, "(", [ expression_list ], ")" ;

(* Имена встроенных функций *)
function_name = "abs"| "min"| "max"| "round"| "ceil"| "floor" ;

(* Константы *)
constant = "Pi" | "Euler" ;

(* Идентификаторы *)
identifier = ( letter | "_" ), { letter | digit | "_" } ;

(* Числовые литералы *)
number = [ "+" | "-" ], digit, { digit }, [ ".", digit, { digit } ] ;

letter = "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J"
| "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T"
| "U" | "V" | "W" | "X" | "Y" | "Z"
| "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j"
| "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t"
| "u" | "v" | "w" | "x" | "y" | "z" ;

(* Цифры *)
digit = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;

```