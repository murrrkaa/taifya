# Грамматика выражений языка

## Синтаксис выражений
### Выражения могут содержать

- Числовые литералы
- Идентификаторы (переменные, константы)
- Арифметические операторы
- Скобки для группировки выражений
- Вызовы встроенных функций со списком аргументов

## Операторы
### Арифметические операторы
| Символ  | Операция                  |
|---------|---------------------------|
| `+`     | Сложение, унарный плюс    |
| `-`     | Вычитание, унарный минус  |
| `*`     | Умножение                 |
| `/`     | Деление                   |

## Приоритет операторов

| Приоритет (по убыванию) | Операторы               |
|-------------------------|-------------------------|
| 5                       | `+`, `-`, `!` (унарные) |
| 4                       | `*`, `/`                |
| 3                       | `+`, `-` (бинарные)     |
| 2                       | `and`                   |
| 1                       | `or`                    |


## Встроенные функции

| Функция          | Назначение                                                 |
|------------------|------------------------------------------------------------|
| `abs(x)`         | Возвращает модуль числа                                    |
| `min(a, b, ...)` | Возвращает наименьшее из переданных чисел                  |
| `max(a, b, ...)` | Возвращает наибольшее из переданных чисел                  |
| `round(x)`       | Округляет число до ближайшего целого                       |
| `ceil(x)`        | Возвращает ближайшее целое, большее или равное переданному |
| `floor(x)`       | Возвращает ближайшее целое, меньшее или равное переданному |
| `readInt()`      | Считывает число                                            |

## Поддержка констант

| Константа   | Значение     | Назначение      |
|-------------|--------------|-----------------|
| `Pi`        | 3.1415926535 | Число π         |
| `Euler`     | 2.7182818284 | Число Эйлера e  |

## Грамматика в нотации EBNF

```ebnf

(* Список выражений *)
expression_list = expression, { ",", expression } ;

(* Основное выражение: операции присваивания и логического ИЛИ *)
expression = assignment_statement | logical_or_expression ;

(* Логическое ИЛИ *)
logical_or_expression = logical_and_expression, { "or", logical_and_expression } ;

(* Логическое И *)
logical_and_expression = comparison_expression, { "and", comparison_expression } ;

(* Сравнения: ==, !=, <, <=, >, >= *)
comparison_expression = additive_expression,
                        [ ("==" | "!=" | "<" | "<=" | ">" | ">="), additive_expression ] ;

(* Операции сложения и вычитания *)
additive_expression = term_expression, { ("+" | "-"), term_expression } ;

(* Термы — операции умножения и деления *)
term_expression = factor_expression, { ("*" | "/"), factor_expression } ;

(* Факторы — унарные операции + и - и НЕ *)
factor_expression = [ "+" | "-" | "!" ], simple_expression ;

(* Простые выражения — числа, идентификаторы, функции, константы или подвыражения в скобках *)
simple_expression = number | constant | function_call | identifier | "(", expression, ")" ;

(* Вызовы функций *)
function_call = (built_in_function | identifier), "(", [ expression_list ], ")" ;

(* Имена встроенных функций *)
built_in_function = "abs"| "min"| "max"| "round"| "ceil"| "floor" | "readInt";

(* Константы *)
constant = "Pi" | "Euler" ;

(* Идентификаторы *)
identifier = ( letter | "_" ), { letter | digit | "_" } ;

(* Числовые литералы *)
number = digit, { digit }, [ ".", digit, { digit } ] ;

letter = "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J"
| "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T"
| "U" | "V" | "W" | "X" | "Y" | "Z"
| "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j"
| "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t"
| "u" | "v" | "w" | "x" | "y" | "z" ;

(* Цифры *)
digit = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;

```